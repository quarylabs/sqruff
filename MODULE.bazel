"""Bazel module definition for sqruff."""

module(
    name = "sqruff",
    version = "0.0.0",
)

bazel_dep(name = "aspect_bazel_lib", version = "2.22.0")

# Platform-aware tool downloads (ratchet, cargo-machete, cargo-hack, uv)
tools = use_extension("//:tools.bzl", "tools")
use_repo(tools, "cargo_hack", "cargo_machete", "ratchet", "uv")

bazel_dep(name = "aspect_rules_js", version = "2.3.7")
bazel_dep(name = "rules_nodejs", version = "6.3.4")
bazel_dep(name = "rules_playwright", version = "0.5.3")
bazel_dep(name = "rules_python", version = "1.7.0")

# Python toolchain for hermetic Python builds (required by pyo3)
python = use_extension("@rules_python//python/extensions:python.bzl", "python")
python.toolchain(
    is_default = True,
    python_version = "3.12",
)
use_repo(python, "python_3_12", "python_versions")
register_toolchains("@python_versions//:all")
bazel_dep(name = "aspect_rules_lint", version = "1.13.0")
bazel_dep(name = "rules_rust", version = "0.68.1")
bazel_dep(name = "rules_rust_wasm_bindgen", version = "0.68.1")
bazel_dep(name = "rules_shell", version = "0.6.1")
bazel_dep(name = "rules_shellcheck", version = "0.4.0")

node = use_extension("@rules_nodejs//nodejs:extensions.bzl", "node")
node.toolchain(node_version = "22.12.0")

npm = use_extension("@aspect_rules_js//npm:extensions.bzl", "npm")
npm.npm_translate_lock(
    name = "npm",
    data = [
        "//:package.json",
        "//:pnpm-workspace.yaml",
        "//playground:package.json",
        "//editors/code:package.json",
    ],
    pnpm_lock = "//:pnpm-lock.yaml",
)
use_repo(npm, "npm")

pnpm = use_extension("@aspect_rules_js//npm:extensions.bzl", "pnpm")
pnpm.pnpm(
    name = "pnpm",
    pnpm_version = "10.0.0",
)
use_repo(pnpm, "pnpm")

# Playwright browsers for e2e testing
playwright = use_extension("@rules_playwright//playwright:extensions.bzl", "playwright")
playwright.repo(
    name = "playwright",
    playwright_version = "1.57.0",
)
use_repo(playwright, "playwright")

# Rust toolchain
rust = use_extension("@rules_rust//rust:extensions.bzl", "rust")
rust.toolchain(
    edition = "2024",
    extra_target_triples = ["wasm32-unknown-unknown"],
    versions = ["1.92.0"],
)
use_repo(rust, "rust_toolchains")

register_toolchains("@rust_toolchains//:all")

# Required for WASM cross-compilation
register_toolchains("@rules_rust//rust/private/dummy_cc_toolchain:dummy_cc_wasm32_toolchain")

# Crate universe - reads from Cargo.toml workspace
crate = use_extension("@rules_rust//crate_universe:extensions.bzl", "crate")
crate.from_cargo(
    name = "crates",
    cargo_lockfile = "//:Cargo.lock",
    manifests = [
        "//:Cargo.toml",
        "//crates/cli:Cargo.toml",
        "//crates/cli-lib:Cargo.toml",
        "//crates/cli-python:Cargo.toml",
        "//crates/lib:Cargo.toml",
        "//crates/lib-core:Cargo.toml",
        "//crates/lib-dialects:Cargo.toml",
        "//crates/lib-wasm:Cargo.toml",
        "//crates/lineage:Cargo.toml",
        "//crates/lsp:Cargo.toml",
        "//crates/sqlinference:Cargo.toml",
    ],
)

# Annotations for pyo3 crates - use Python toolchain for build scripts
crate.annotation(
    crate = "pyo3-build-config",
    build_script_toolchains = ["@rules_python//python:current_py_toolchain"],
)
crate.annotation(
    crate = "pyo3-ffi",
    build_script_toolchains = ["@rules_python//python:current_py_toolchain"],
)

use_repo(crate, "crates")
